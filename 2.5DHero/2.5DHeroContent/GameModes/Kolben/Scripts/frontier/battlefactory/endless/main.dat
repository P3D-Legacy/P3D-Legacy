version=2
@Text.Show(Welcome to the Battle Factory!*I oversee trainer's individual~challenges.*This is the~Endless Mode.~If you would like to enter~the factory, you must have~only 1 Pokémon.*Also, you have to save~your game before entering.*Would you like to~challenge the Factory?)
@Options.Show(Yes,No)
:select:<options.result>
    :when:Yes
        @system.save
        @sound.play(Save)
        @text.show(<player.name>~saved the game.)
        @storage.set(boolean,thirdperson,<camera.thirdperson>)

        :if:<pokemon.count>>1
            @Text.Show(Please remove <math.int(<pokemon.count>-1)> Pokémon~from your party.)
            @Screen.StorageSystem
        :endif

        :if:<register.exists(frontier_battlefactory_endless_values_created)>=false
            @register.new(battlefactory_endless_50_class,INT,0)
            @register.new(battlefactory_endless_50_best,INT,0)
            @register.new(battlefactory_endless_100_class,INT,0)
            @register.new(battlefactory_endless_100_best,INT,0)
            @register.new(frontier_battlefactory_endless_values_created)
        :endif      

        @storage.set(factory_streak,0)

        :if:<pokemon.count>=1 <and> <pokemon.countbattle>=1
            @text.show(Which challenge do~you want to start?)
            @options.show(Lv. 50,Lv. 100,Cancel)
            :select:<options.result>
                :when:Lv. 50
                    @storage.set(factory_level,50)
                    @storage.set(factory_class,<register.value(battlefactory_endless_50_class)>)
                :when:Lv. 100
                    @storage.set(factory_level,100)
                    @storage.set(factory_class,<register.value(battlefactory_endless_100_class)>)
                :when:Cancel
                    @Text.Show(Well, come back and see~us again when you have*prepared yourself for~the challenge.)
                    :exit
            :endselect
            
            @pokemon.heal
            @storage.set(factory_pokemonstore,<pokemon.data(0)>)
            @pokemon.clear

            @pokemon.add(<pokemon.generatefrontier(<storage.get(factory_level)>,<storage.get(factory_class)>)>)
            @pokemon.add(<pokemon.generatefrontier(<storage.get(factory_level)>,<storage.get(factory_class)>)>)
            @pokemon.add(<pokemon.generatefrontier(<storage.get(factory_level)>,<storage.get(factory_class)>)>)
            
            @Text.Show(Well, now you are~prepared.)
            @pokedex.setautodetect(0)

            @script.start(frontier\battlefactory\endless\intro_walk)
        :else
            @Text.Show(It seems you have~too many Pokémon.*Please return when you are~prepared to use exactly~1 Pokémon.)
        :endif
    :when:No
        @Text.Show(Well, come back and see~us again when you have*prepared yourself for~the challenge.)
:endselect
:end