version=2

	@storage.set(str,billspcname,Someone's PC)
	:if:<register.registered(bill_encounter)>=true
		@storage.set(str,billspcname,Bill's PC)
	:endif
	
	:if:<register.registered(pokemongot)>=true
		@storage.set(int,canind,2)
		@storage.set(str,computer_options,<storage.get(str,billspcname)>,Inbox)
	:else
		@storage.set(int,canind,1)
		@storage.set(str,computer_options,Inbox)
	:endif

	:if:<player.haspokedex>=true
		@storage.set(str,computer_options,<storage.get(str,computer_options)>,Prof. Oak's PC)
		@storage.set(int,canind,<storage.get(int,canind)>+1)
	:endif
	
	:if:<pokemon.counthalloffame>>0
		@storage.set(str,computer_options,<storage.get(str,computer_options)>,Hall of Fame)
		@storage.set(int,canind,<storage.get(int,canind)>+1)
	:endif
	
	@storage.set(str,computer_options,<storage.get(str,computer_options)>,Mystery Events)
	@storage.set(int,canind,<storage.get(int,canind)>+1)
	
	:if:<player.isgamejolt(1)>=true
		@storage.set(str,computer_options,<storage.get(str,computer_options)>,Daily Rewards)
		@storage.set(int,canind,<storage.get(int,canind)>+1)
	:endif

	@storage.set(str,computer_options,<storage.get(str,computer_options)>,Log off)
	@options.setcancelindex(<storage.get(int,canind)>)
	
	@options.show(<storage.get(str,computer_options)>)
		:when:<storage.get(str,billspcname)>
			@sound.play(PC\LogOn)
			@screen.storagesystem
			@script.start(computer_choose)
		:when:Mystery Events
			@sound.play(PC\LogOn)
			@screen.mysteryevent
			@script.start(computer_choose)
		:when:Inbox
			@sound.play(PC\LogOn)
			@screen.mailsystem
			@script.start(computer_choose)
		:when:Prof. Oak's PC
			@sound.play(PC\LogOn)
			@script.start(pallet\evaldist)
			@text.show(Choose a host to~connect to.)
			@script.start(computer_choose)
		:when:Hall of Fame
			@sound.play(PC\LogOn)
			@screen.halloffame
			@script.start(computer_choose)
		:when:Daily Rewards
			@sound.play(PC\LogOn)
			@script.start(startscript\pcreward)
			@script.start(computer_choose)
		:when:Log off
			@sound.play(PC\TurnOff)
			:endscript
	:endwhen

:end
